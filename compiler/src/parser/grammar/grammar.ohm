LawScript {
  program = program_content+
  
  program_content = blank (section|  rule | annotation | statement) blank
  
  section_content = blank (section|  rule | annotation | statement) blank
  
  
  statement = (import_stm | declaration| alias | assignation |  enum | instruction) blank ";"
  
  import_stm = "include" blank string

  alias = alias_name blank "is" blank property_value
  assignation = declaration_name blank "=" blank property_value

  declaration = declaration_name blank ":" blank property_value blank decorator*
  
  // @ is legacy
  decorator = blank "@"? decorator_name blank "=" blank decorator_value
  
  decorator_name = word_or_num
  decorator_value = number | string

  // "Dictionary" is legacy
  enum = ("dictionary" | "Dictionary") blank enum_name blank "{" blank enumerals blank "}"

  comment = comment_inline | comment_block
  comment_inline = spaces comment_inline_signal comment_line eol
  comment_inline_signal = ("//" | "#" )

  comment_line = (~eol any)+
  comment_block = comment_open comment_content comment_close
  comment_open = "/*"
  comment_close = "*/"
  comment_content = (~comment_close any)+

  enum_name = word_no_dot
  enumerals = enumeral enumeral_sub*
  enumeral = enumeral_name blank ":" blank enumeral_properties
  enumeral_properties = property_object
  enumeral_sub = blank ("," | newLine) blank enumeral
  enumeral_name = word_no_dot


  alias_name = word

  
  declaration_name = word

  // @ is legacy
  section = ("section" | "@") blank section_name blank "{" blank section_content* blank "}"
  section_name = word

 
  // "()" is legacy
  rule = rule_type blank rule_name blank "()"? blank "{" blank statement blank "}"
  // Rule is legacy
  rule_type = "Rule" | "rule" | "macro"
  rule_name = word
  rule_content = (import_stm | annotation)* blank statement*

  instruction = (annotation)* blank instruction_content*

  instruction_content = instruction_operation | chain_item
  instruction_operation = instruction_content (instruction_operation_next)+
  instruction_operation_next =  blank instruction_operator blank chain_item
  instruction_operator =  or | and | time_operator | causal_operator

   chain_item = (instruction_operation | situation_base_not | situation_base) (blank annotation)*

  situation_base = (section | rule_call | block | logic_block | variable) blank annotation*
  situation_base_not = not (rule_call | block | logic_block | variable)

  logic_block = "(" blank instruction  blank ")"
  
  causal_operator = "-->" | "cause"
  time_operator = "==>" | "before"
  time_point_operator = "on"
  or = "||" | "or"
  and = "&&" | "and"
  not = "!" | "not"
 
  block =  block_name blank "{" blank block_content? blank "}"
  block_name = word

 

  block_content = property block_content_sub*
  block_content_sub = blank ("," | newLine) blank property

  property = property_name blank ":" blank property_value blank decorator*
  property_name = word
  property_value = block | mathematical_expression | property_object | property_list
  property_list = blank "[" blank list_item property_list_following_items* blank "]" blank
  // not ideal but prevent a bug in parsing
  property_list_following_items = blank "," blank list_item
  
  range = range_short_notation | range_dot_notation
  range_dot_notation = "[" range_equals_min? blank range_from?  blank "..." blank range_to? blank range_equals_max?  "]"
  range_short_notation = "[" range_operators mathematical_expression "]"
  range_from = (string | number | variable | mathematical_expression)
  range_to = (string | number | variable | mathematical_expression)
  range_equals_min = "="
  range_equals_max = "="


  list_item = string | number | variable | property_object

  variable =  variable_name unit?
  variable_name =  word sub_variable*
  sub_variable = "." word
  property_object = "{" blank block_content? blank "}"
  
 
 
  annotation = blank ("annotation" | "::") annotation_negation? blank (block | block_name) blank
  annotation_negation = "!" | "not"

  mathematical_expression = mathematical_expression_test | mathematical_expression_block | mathematical_expression_unary_operation | mathematical_expression_operation | number | variable | range | string
  mathematical_expression_no_test =  mathematical_expression_block | mathematical_expression_unary_operation_no_test | mathematical_expression_operation_no_test | number | variable | range | string

  mathematical_expression_unary_operation = blank mathematical_expression_unary_operator blank mathematical_expression
  mathematical_expression_unary_operation_no_test = blank mathematical_expression_unary_operator blank mathematical_expression_no_test
  

  mathematical_expression_unary_operator = mathematical_expression_operator_not

  mathematical_expression_operator_not = "!" | "not"

  mathematical_expression_operation = blank mathematical_expression blank mathematical_expression_operator blank mathematical_expression blank
  mathematical_expression_operation_no_test = blank mathematical_expression_no_test blank mathematical_expression_operator blank mathematical_expression_no_test blank

  
  mathematical_expression_block = "(" mathematical_expression ")"

  mathematical_expression_operator = mathematical_expression_operator_add | mathematical_expression_operator_substract | mathematical_expression_operator_divide | mathematical_expression_operator_multiply |  mathematical_expression_operator_exponent | mathematical_expression_operator_percentOf | mathematical_expression_operator_or |  mathematical_expression_operator_and | mathematical_expression_operator_strictly_bigger | mathematical_expression_operator_bigger | mathematical_expression_operator_strictly_lower | mathematical_expression_operator_equal | mathematical_expression_operator_different

  mathematical_expression_operator_add = "+" | "plus"
  mathematical_expression_operator_substract = "-" | "minus"
  mathematical_expression_operator_divide = "/" | "divided by" | "over" | "per" | "out of"
  mathematical_expression_operator_multiply = "*" | "times" | "multiplied by" | "x"| "by"
  mathematical_expression_operator_exponent = "^" | "to the power of" | "power" | "pow" | "exp"
  mathematical_expression_operator_percentOf = "%of" | "percent of"
  mathematical_expression_operator_and ="&&" | "and"
  mathematical_expression_operator_or = "||" | "or"

  range_operators = mathematical_expression_operator_bigger 
  | mathematical_expression_operator_strictly_bigger 
  | mathematical_expression_operator_lower 
  | mathematical_expression_operator_strictly_lower
  
  mathematical_expression_operator_strictly_bigger = ">" | "greater than" | "greater to" | "greater" | "bigger" | "bigger than" | "bigger to"
  mathematical_expression_operator_bigger = ">=" | "greater or equal than" | "greater or equal to" | "greater or equal" | "greater or equals" | "bigger or equal" | "bigger or equals" | "bigger or equal than" | "bigger or equal to" | "bigger or equals to"
  mathematical_expression_operator_strictly_lower = "<" | "lower than" | "lower to" | "lower" | "smaller" | "smaller than" | "smaller to"
  mathematical_expression_operator_lower = "<=" | "lower or equal than" | "lower or equal to" | "lower or equal" | "lower or equals" | "smaller or equal" | "smaller or equals" | "smaller or equal than" | "smaller or equal to" | "smaller or equals to"
  mathematical_expression_operator_equal = "==" | "equals" | "equal" | "is" | "are" | "is equal to" | "are equal to" | "is equals to" | "are equals to" | "is equal" | "are equal" | "is equals" | "are equals"
  mathematical_expression_operator_different = "!=" | "different" | "different from" | "is different" | "are different" | "is different from" | "are different from" | "is not" | "are not" | "is not equal" | "are not equal" | "is not equals" | "are not equals" | "is not equal to" | "are not equal to" | "is not equals to" | "are not equals to"


  mathematical_expression_test =  mathematical_expression_test_condition blank "?" blank  mathematical_expression_test_if_true blank ":" blank mathematical_expression_test_if_false
  mathematical_expression_test_condition = mathematical_expression_no_test 
  mathematical_expression_test_if_true = mathematical_expression
  mathematical_expression_test_if_false = mathematical_expression


  rule_call = rule_call_reference | rule_call_traditional

  rule_call_traditional = rule_name blank "(" blank ")"
  rule_call_reference = ("ยง"|"$") rule_name

  string = "\"" string_content "\""
  string_content = (~"\"" any)*
  
  unit = blank variable_name

  number = real_number unit?
  real_number = blank number_value
  number_value = number_traditional_notation | number_dot_notation
  

  
  number_traditional_notation = number_beforedot number_afterdot?
  number_dot_notation = number_afterdot
  number_beforedot = digit+
  number_afterdot = "." digit+ 
    
  word = letter (alnum | "_" | ".")*
  word_or_num = (alnum | "_" | "." | "-")*
  word_no_dot = letter (alnum | "_")*
  
  blank = spaceoreol*
  spaceoreol = (space | "\n" | "\r" | comment)
  eol = ~" " (space | end)

  newLine = blank ("\r\n" | "\n")
}