include "./ontology/dictionary-Event.ipdl";
include "./ontology/dictionary-ObjectRelation.ipdl";
include "./ontology/Situation.ipdl";

class Person {

};

class Venue {

};

class Gathering {

};

class PhysicalObject {
};

class Amount {

};



Person Spouse1;
Person Spouse2;

// how to do alliases
alias PolicyHolder {
  is : [
    Spouse1,
    Spouse2
  ]
};

Venue BookedVenue;
Gathering Wedding;
Gathering WeddingReception;

PhysicalObject InsuredMarquee;
Amount InsuredSum;



// Not in Contract, added for test
/*Chain Main() {
  ::Payout {
    type: "Limit",
    amount: 20000000,
    unit: "USD"
  }

  *
}*/


Chain Cancellation() {

  ::Text {
    value: " # Part I – Cancellation
We will pay up to the amount shown in the schedule in total for any irrecoverable expenses you have paid for or which you have to pay for, under contract or subsequent agreement for the services of any wedding services supplier not used as a direct result of the unavoidable cancellation by you of the wedding or wedding reception caused by any of the following reasons:"
  }

  ::Payout {
    type: "Limit",
    amount: InsuredSum,
    unit: "USD"
  }

  CancellationDueToVenue()
  || CancellationDueToDeathOrInjury()
  || CancellationDueToSupplier()
  || CancellationDueToRedundancy()
  || CancellationDueToOverseaPublicService()
  || CancellationDueToNonApparenceOfMinister()
  || CancellationDueToCatNat()
  || CancellationDueToMarquee()
  <--
  Situation {
    event: Event.UnavoidableCancellation,
    relation: [{
      type: Relation.HappenTo,
      target: Wedding || WeddingReception
    },
    {
      type: Relation.CausedBy,
      target: PolicyHolder
    }]
  }
  <--  
  /*Special claims conditions relating to Section 1, Part I – In the event of a valid claim for cancellation there must be a clear intention to rearrange the wedding at a later date except where the cancellation is caused by your death.*/
  Situation {
    event: Event.IntentionToReplan,
    relation: [
      {
        type: Relation.HappenTo,
        target: Wedding || WeddingReception
      },
      {
        type: Relation.By,
        target: PolicyHolder
      }
    ]
  } 
}

Rule CancellationDueToVenue() {
  ::Text {
    value: " 1. the booked venue for the wedding or wedding reception being unable to hold your wedding and/or wedding reception due to:
  a) ceasing to trade due to financial failure, bankruptcy, liquidation or administration
  b) damage to the venue caused by fire, natural catastrophe or adverse weather, 
  c) murder, death or suicide at the venue
  d) an act of terrorism at the venue
  e) closure by a relevant authority;"
  }

  Situation {
    event: Event.Closure,
    relation: [{
      type: Relation.HappenTo,
      target: BookedVenue
    }, {
      type: Relation.CausedBy,
      target: "relevant authority"
    }]
  } ::Probability{p: 0.0000001}
  ||
  Situation {
    event: Event.ActOfTerrorism,
    relation: {
      type: Relation.HappenAt,
      target: BookedVenue
    }
  } ::Probability{p: 0.0000001}
  ||
  Situation {
    event: Event.Murder||Event.Death|| Event.Suicide,
    relation: {
      type: Relation.HappenAt,
      target: BookedVenue
    }
  } ::Probability{p: 0.00001}
  ||
  (
    Situation {
      event: Event.Fire || Event.NaturalCatastrophe || Event.AdverseWeather
    }
    <--
    Situation {
      event:Event.Damage,
      relation: {
        type: Relation.HappenTo,
        target: BookedVenue
      }
    } ::Probability{p: 0.0001}
  )
  || 
  Situation {
    event: Event.FinancialFailure || Event.Bankruptcy || 
    Event.Liquidation || Event.Administration,
    relation: {
      type: Relation.HappenTo,
      target: BookedVenue
    }
  } ::Probability{p: 0.001}
  <--
  Situation {
    event: Event.UnableToHoldGathering,
    relation: [{
      type: Relation.By,
      target: BookedVenue
    },
    {
      type: Relation.HappenTo,
      target: Wedding || WeddingReception
    }]
  } ::Probability{p: 0.3}
}


Rule CancellationDueToDeathOrInjury(){

  ::Text {
    value: "  2. the death, injury or sickness of one or both of the couple or their close relative which would make having or continuing with the wedding and/or wedding reception inappropriate;"
  }
  Situation {
    event: Event.Death ||  Event.Injury || Event.Sickness,
    relation: {
      type: Relation.HappenTo,
      target: PolicyHolder.Spouse1 || PolicyHolder.Spouse2
      /* // TODO|| {
        type: Relation.CloseRelative,
        target: "PolicyHolder && self"
      }*/
    }
  } ::Probability{p: 0.00001}
  <--
  Situation {
    event: Event.InapropriateCircumpstance,
    relation: {
      type: Relation.HappenTo,
      target: Wedding || WeddingReception
    }
  } ::Probability{p: 0.0001}

}


Rule CancellationDueToSupplier() {
 ::Text{
   value : "3. the total non-appearance of any booked and paid for professional Wedding Services Supplier which would make having or continuing with the wedding or wedding reception impossible;"
 }

 Situation {
   event: Event.NoShow,
   relation: {
     type: Relation.CausedBy,
     target: "booked and paid for professional Wedding Services Supplier"
   }
 } ::Probability{p: 0.001}
 <--
 Situation {
   event: Event.ImpossibleContinuationOfEvent || Event.ImpossibleHappeningOfEvent,
   relation: {
     type:  Relation.HappenTo,
     target: Wedding || WeddingReception
   }
 } ::Probability{p: 0.0001}
}


Rule CancellationDueToRedundancy() {

  ::Text {
    value: "4. your redundancy or that of any of your close relatives who have or would have made proven, significant, financial contributions on which the wedding arrangements depend, where notice is received at least 16 weeks after date of purchase of this insurance and which qualifies for payment under redundancy legislation and where there is a clear intention to rearrange the wedding; "
  }
  Situation {
    event: Event.PolicyPurchase
  }
  <--
  Time {
    value: 16,
    unit: "Week"
  }
  <--
  Situation {
    event: Event.NotificationOfLossOfJob,
      relation: {
        type: Relation.HappenTo,
        target: PolicyHolder 
        /*|| {
          "who have or would have made proven, significant, financial contributions on which the wedding arrangements depend"
        }*/
      }
  } ::Probability{p: 0.00001}

}

Rule CancellationDueToOverseaPublicService() {
  ::Text {
    value: "5. one of the couple or a close relative being unforeseeably posted overseas or being called on unavoidable and necessary duty where that person is a serving member of the UK armed forces, ambulance or health service, coastguard, fire brigade or police force; "
  }
  // Should one of the couple be Policyholder
  Situation {
    event: "mobilized",
    relation: {
      type: Relation.HappenTo,
      target: PolicyHolder.Spouse1 || PolicyHolder.Spouse2 || "couple close relative"
    }
  } ::Probability{p: 0.00001}

}


Rule CancellationDueToNonApparenceOfMinister() {
  ::Text {
    value : "6. the non-appearance of the intended officiating minister or registrar and no substitute can be obtained;"
  }
  Situation {
    event: Event.NoShow,
    relation: {
      type: Relation.CausedBy,
      target: "intended officiating minister or registrar"
    }
  } ::Probability{p: 0.00001}
}


Rule CancellationDueToCatNat() {

  ::Text {
    value: "7. your inability or that of at least 50% of the guests to reach the wedding or wedding reception venue due to adverse weather conditions or natural  catastrophe;"
  }

  Situation {
    event: Event.AdverseWeather || Event.NaturalCatastrophe
  } ::Probability{p: 0.00001}
  <--
  Situation {
    event: Event.InabilityToReachPlace,
    relation: [{
      type: Relation.HappenTo,
      target: PolicyHolder || "at least 50% of the guests"
    },{
      type: Relation.HappenAt,
      target: Wedding || WeddingReception
    }
    ]
  }
}

Rule CancellationDueToMarquee() {

  ::Text {
    value: "8. loss or theft of or severe damage to the marquee (only applies if you have purchased the optional marquee cover)."
  }
  Situation {
    event: Event.Loss || Event.Theft,
    relation: {
      type: Relation.HappenTo,
      target: InsuredMarquee
    }
  } ::Probability{p: 0.0001}
}


