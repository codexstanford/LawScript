IPDL {
  program = (comment | spaceoreol | import_stm | declaration | rule | annotation)+


  import_stm = "include" blank string blank ";"

  comment = comment_inline | comment_block
  comment_inline = spaces comment_inline_signal comment_line eol
  comment_inline_signal = ("//" | "#")

  comment_line = (~eol any)+
  comment_block = comment_open comment_content comment_close
  comment_open = "/*"
  comment_close = "*/"
  comment_content = (~comment_close any)+
  
  declaration = declaration_type blank (block_declaration | declaration_name)  blank ";"
  declaration_type = word
  declaration_name = word

  block_declaration = block_name blank extend_list?  "{" blank block_content? blank "}"
  extend_list = "extends" blank block_extends blank
  block_extends = word

  rule = rule_type blank rule_name blank "()" blank "{" blank rule_content blank "}"
  rule_type = "Rule" | "Chain"
  rule_name = word
  rule_content = annotation* blank situation*

  situation = situation_causal
  situation_causal = operation_causal | situation_or
  operation_causal = situation_causal situation_causal_next+
  situation_causal_next = blank causal_operator blank situation_or
  situation_or = operation_or | situation_base_not | situation_base
  operation_or = situation_or situation_or_next+
  situation_or_next = blank or blank (situation_base_not | situation_base)
  situation_base = (rule_call | block | logic_block | wildcard) blank annotation*
  situation_base_not = not (rule_call | block | logic_block)

  logic_block = "(" blank rule_content blank ")"
  
  causal_operator = "<--"
  or = "||"
  and = "&&"
  not = "!"
 
  block = block_name blank "{" blank block_content? blank "}"
  block_name = word
 
  block_content = property block_content_sub*
  block_content_sub = blank "," blank property

  property = property_name blank ":" blank property_value 
  property_name = word
  property_value = expression | string | number | variable | property_object | property_list
  
  property_list = blank "[" blank list_item (blank "," blank list_item)* blank "]" blank

  list_item = string | number | variable | property_object
 
  expression = (expression_not_value | expression_value) expression_sub+
  expression_sub = blank expression_operand blank (expression_not_value | expression_value)
  expression_value = string | number | variable

  expression_not_value = not expression_value
  


  expression_operand = or | and

  variable = word sub_variable*
  sub_variable = "." word
  property_object = "{" blank block_content blank "}"
  
 
 
  annotation = "::" block blank
 
  wildcard = "*"
  

  
  rule_call = rule_name blank "(" blank ")"
  
  string = "\"" string_content "\""
  string_content = (~"\"" any)*
   
  number = (number_traditional_notation | number_dot_notation)
  number_traditional_notation = number_beforedot number_afterdot?
  number_dot_notation = number_afterdot
  number_beforedot = digit+
  number_afterdot = "." digit+ 
    
  word = letter (alnum | "_" | ".")*
  blank = spaceoreol* 
  spaceoreol = (space | "\n" | "\r" | comment)
  eol = ~" " (space | end)  
 
}